# Maintainer: Akash6222
# Project: TCET Linux

pkgname=tcet-linux-tk
pkgver=8.6.13
pkgrel=2
pkgdesc="A windowing toolkit for use with tcl"
arch=('x86_64')
url="https://github.com/tcet-opensource/tcet-linux-tk"
license=('custom')
depends=('tcl' 'libxss' 'libxft')
options=('staticlibs')
source=("$pkgname".tar.gz::"${url}"/releases/download/latest/"${pkgname}".tar.gz)
sha256sums=('87bc1dc4aa772260ae8e1083835c4c12809a4c23ec67e9c93fad64ce9c8a830d')

build() {
  cd $pkgname/unix
  ./configure --prefix=/usr --mandir=/usr/share/man --enable-threads --disable-rpath --enable-64bit
  make
}

check() {
  cd $pkgname/unix
#  make test
}

package() {
  cd $pkgname/unix
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf wish${pkgver%.*} "${pkgdir}/usr/bin/wish"
  ln -sf libtk${pkgver%.*}.so "${pkgdir}/usr/lib/libtk.so"
  install -Dm644 license.terms "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # install private headers (FS#14388, FS#47616)
  cd ..
  for dir in compat generic generic/ttk unix; do
    install -dm755 "${pkgdir}"/usr/include/tk-private/$dir
    install -m644 -t "${pkgdir}"/usr/include/tk-private/$dir $dir/*.h
  done

  # remove buildroot traces
  sed -e "s#${srcdir}/tk${pkgver}/unix#/usr/lib#" \
      -e "s#${srcdir}/tk${pkgver}#/usr/include#" \
      -i "${pkgdir}/usr/lib/tkConfig.sh"

  # remove unrequired execute permissions (FS#75042)
  chmod -x "${pkgdir}/usr/lib/libtkstub8.6.a"
}

